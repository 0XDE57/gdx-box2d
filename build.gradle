def prop(String key) {
  hasProperty(key) ? property(key).toString() : null
}

allprojects {
  repositories {
    mavenCentral()
  }
  group = 'org.aya-prover.gdx-box2d'
  version = '1.3.0'
}

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  apply plugin: 'signing'

  def ossrhUsername = prop("ossrhUsername")
  def ossrhPassword = prop("ossrhPassword")

  if (ossrhUsername != null) publishing.repositories {
    maven {
      url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2")
      name = "MavenCentral"
      credentials {
        username = ossrhUsername
        password = ossrhPassword
      }
    }
  }

  tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
  }

  tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier = 'javadoc'
    from javadoc
  }

  javadoc.options {
    addStringOption('Xdoclint:none', '-quiet')
  }

  artifacts {
    archives sourcesJar, javadocJar
  }
  java {
    withSourcesJar()
    withJavadocJar()
  }

  def projName = project.name
  def publish = projName.startsWith('gdx-box2d') && !projName.contains('test')
  if (publish) publishing.publications {
    maven(MavenPublication) {
      artifactId(projName.drop('gdx-box2d-'.length()))
      groupId project.group
      version project.version
      from components.java

      pom {
        name = artifactId
        description = 'This project is a higher-order fork of a box2d binding.'
        url = 'https://github.com/ice1000/gdx-box2d'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = 'ice1000'
            name = 'Tesla Zhang'
            email = 'ice1000kotlin@foxmail.com'
          }
          developer {
            id = 'MobiDevelop'
            name = 'Justin Shapcott'
            email = 'justin@mobidevelop.com'
          }
        }
        scm {
          url = 'https://github.com/ice1000/gdx-box2d'
          connection = "scm:git:${url}"
        }
      }
    }
  }

  if (publish) signing {
    sign publishing.publications.maven
  }
}