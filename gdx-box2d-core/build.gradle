import box2d.Deps

buildscript {
  repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
  }
  dependencies { classpath "com.badlogicgames.gdx:gdx-jnigen-gradle:2.3.1" }
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories { mavenCentral() }
dependencies { implementation Deps.gdx }
apply plugin: "com.badlogicgames.gdx.gdx-jnigen"

jnigen {
  sharedLibName = "gdx-box2d"

  all {
    headerDirs = ["src/box2d-2.4.1/include"]
    cIncludes = ["*.c", "src/box2d-2.4.1/src/**/*.c"]
    cFlags += " -fvisibility=hidden "
    cppIncludes = ["*.cpp", "src/box2d-2.4.1/src/**/*.cpp"]
    cppFlags += " -fvisibility=hidden -std=c++11 "
  }

  add(Windows, x32)
  add(Windows, x64)
  add(Linux, x32)
  add(Linux, x64)
  add(Linux, x32, ARM)
  add(Linux, x64, ARM)
  add(MacOsX, x64)
  add(Android)
  add(IOS) {
    cppFlags += " -stdlib=libc++ "
  }

}

clean.doFirst {
  delete fileTree("jni") {
    include "jni-headers"
    include "target"
    include "build*.xml"
    include "*.c"
    include "*.cpp"
    include "*.h"
    include "*.mk"
    include "robovm.xml"
  }
  delete "libs"
  delete "obj"
}

//jar.dependsOn jnigenBuild
sourceSets.main.resources.srcDirs += ["libs"]

tasks.register('sourcesJar', Jar) {
  dependsOn classes
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
  dependsOn javadoc
  archiveClassifier = 'javadoc'
  from javadoc
}

javadoc.options {
  addStringOption('Xdoclint:none', '-quiet')
}

artifacts {
  archives sourcesJar, javadocJar
}
java {
  withSourcesJar()
  withJavadocJar()
}

def prop(String key) {
  hasProperty(key) ? property(key).toString() : null
}

def ossrhUsername = prop("ossrhUsername")
def ossrhPassword = prop("ossrhPassword")

if (ossrhUsername != null) publishing.repositories {
  maven {
    url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2")
    name = "MavenCentral"
    credentials {
      username = ossrhUsername
      password = ossrhPassword
    }
  }
}

publishing.publications {
  maven(MavenPublication) {
    artifactId 'core'
    groupId 'org.aya-prover.gdx-box2d'
    version project.version
    from components.java

    pom {
      name = artifactId
      description = 'This project is a higher-order fork of a box2d binding.'
      url = 'https://github.com/ice1000/gdx-box2d'
      licenses {
        license {
          name = 'The Apache License, Version 2.0'
          url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
        }
      }
      developers {
        developer {
          id = 'ice1000'
          name = 'Tesla Zhang'
          email = 'ice1000kotlin@foxmail.com'
        }
        developer {
          id = 'MobiDevelop'
          name = 'Justin Shapcott'
          email = 'justin@mobidevelop.com'
        }
      }
      scm {
        url = 'https://github.com/ice1000/gdx-box2d'
        connection = "scm:git:${url}"
      }
    }
  }
}

signing {
  sign publishing.publications.maven
}
