import box2d.Deps

buildscript {
  repositories { mavenCentral() }
  dependencies { classpath "com.badlogicgames.gdx:gdx-jnigen-gradle:2.4.0" }
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories { mavenCentral() }
dependencies { api Deps.gdx }
apply plugin: "com.badlogicgames.gdx.gdx-jnigen"

jnigen {
  sharedLibName = "gdx-box2d"

  all {
    headerDirs = ["src/box2c/include"]
    cIncludes = ["*.c", "src/box2c/src/**/*.c"]
    cFlags += " -fvisibility=hidden "
    cppIncludes = ["*.cpp", "src/box2c/src/**/*.cpp"]
    cppFlags += " -fvisibility=hidden -std=c++11 "
  }

  add(Windows, x32)
  add(Windows, x64)
  add(Linux, x32)
  add(Linux, x64)
  add(Linux, x32, ARM)
  add(Linux, x64, ARM)
  add(MacOsX, x64)
  add(MacOsX, x64, ARM)
  add(Android)
  add(IOS) {
    cppFlags += " -stdlib=libc++ "
  }
}

clean.doFirst {
  delete fileTree("jni") {
    include "jni-headers"
    include "target"
    include "build*.xml"
    include "*.c"
    include "*.cpp"
    include "*.h"
    include "*.mk"
    include "robovm.xml"
  }
  delete "libs"
  delete "obj"
}

//jar.dependsOn jnigenBuild
sourceSets.main.resources.srcDirs += ["libs"]
